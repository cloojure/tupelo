

= Making Clojure Even Sweeter

Leiningen coordinates:   

image:http://clojars.org/tupelo/latest-version.svg[ http://clojars.org/tupelo ]

=== API Docs:  http://cloojure.github.io/doc/tupelo/tupelo.core.html

== Overview

Have you ever wanted to do something simple but `clojure.core` doesn't support it? Or, maybe
you are wishing for an enhanced version of a standard function. The goal of `tupelo.core` is to
add support for these convenience features, so that you have a simple way of using either
the enhanced version or the original version.

The goal in using `tupelo.core` is that you can just plop it into any namespace without
having to worry about any conflicts with `clojure.core` functionality. So, both the core functions
and the added/enhanced functions are both available for use at all times. As such, we use
normally use `:refer :all` for `tupelo.core` in our namespaces:

[source,clojure]
----
(ns my.proj
  (:require [tupelo.core    :refer :all]
            [tupelo.datomic :as td]
            ... ))
----

=== tupelo.core - Basic functions

==== The truth is not ambiguous

Clojure marries the worlds of Java and Lisp. Unfortunately, these two worlds have different ideas of
truth, so Clojure accepts both `false` and `nil` as _false_. Sometimes, however, you want to coerce
logical values into literal _true_ or _false_ values, so we provide a simple way to do that:

----
(truthy? arg)
  Returns true if arg is logical true (neither nil nor false);
  otherwise returns false.

(falsey? arg)
  Returns true if arg is logical false (either nil or false);
  otherwise returns false. Equivalent to (not (truthy? arg)).
----
[source,clojure]
----
> (def data [true :a 'my-symbol 1 "hello" \x false nil])
> (filter truthy? data)
[true :a my-symbol 1 "hello" \x]
> (filter falsey? data)
[false nil]
----

==== Convenience in testing seq's

These functions aren't in clojure.core, but people keep writing into the mailing list
wondering where they are. Well, now they are available:

----
(any? pred coll)
  For any predicate & collection, returns true if (pred x) is 
  logical true for any x in coll; otherwise returns false. Like
  clojure.core/some, but returns only true or false.

(not-empty? coll)
  For any collection, returns true if coll contains any items; 
  otherwise returns false. Equivalent to (not (empty? coll)).
----
The unit test shows these functions in action
[source,clojure]
----
(is (= true   (any? odd? [1 2 3] ) ))
(is (= false  (any? odd? [2 4 6] ) ))
(is (= false  (any? odd? []      ) ))

(is (= (map not-empty? ["1"   [1]   '(1)  {:1 1}  #{1} ] )
                       [true  true  true  true    true ]  ))
(is (= (map not-empty? [""     []      '()    {}     #{}    nil   ] )
                       [false  false   false  false  false  false ] ))
----

==== Focus on vectors

Clojure's seq abstraction (and lazy seq's) is very useful, but sometimes you just want
everything to stay in a nice, eager, random-access vector. Here is an easy way to build up
a vector result:

[source,clojure]
----
(conjv coll x)
(conjv coll x & xs)
  For any collection coll and list of values x, appends the x's to 
  collection, always returning the result as a vector.

=> (conjv '(1 2) 3)
[1 2 3]
=> (conjv [1 2] 3 4 5 6)
[1 2 3 4 5 6]
----

Similarly, we may wish to use an eager version of `for` and return the results
in a vector:

[source,clojure]
----
(is (= (forv [x (range 4)] (* x x))
       [0 1 4 9] ))
----

==== Joining together like collections

The `concat` function can sometimes have rather surprising results:
[source,clojure]
----
  (is (= (concat {:a 1} {:b 2} {:c 3} )
               [ [:a 1] [:b 2] [:c 3] ] ))
----
In this example, the user probably meant to merge the 3 maps into one. The
`conj` function can also surprise the user:
[source,clojure]
----
  (is (= (conj [1 2] [3 4])
               [1 2  [3 4] ] ))
----

Here the user probably wanted to get `[1 2 3 4]` back, but instead got a nested
vector by mistake.

Instead of having to wonder if the items to be combined will be merged, nested, or
converted into another data type, we provide the `glue` function to *always*
combine like collections together into a result collection of the same type:

[source,clojure]
----
; Glues together like collections:
(is (= (glue [1 2] [3 4] [5 6])        [1 2 3 4 5 6]    ))
(is (= (glue {:a 1} {:b 2} {:c 3})     {:a 1 :c 3 :b 2} ))
(is (= (glue #{1 2} #{3 4} #{6 5})    #{ 1 2 6 5 3 4}   ))

; If you want to convert to a sorted set or map, just put an empty one first:
(is (= (glue (sorted-map) {:a 1} {:b 2} {:c 3})   {:a 1 :b 2 :c 3} ))
(is (= (glue (sorted-set) #{1 2} #{3 4} #{6 5})  #{ 1 2 3 4 5 6}   ))
----

An `Exception` will be thrown if the collections to be 'glued' are not all of
the same type:

  - mixed lists & vectors
  - all map (sorted or not)
  - all set (sorted or not)

==== Map Value Lookup

Maps are convenient, especially when keywords are used as functions to look up a value in
a map.  Unfortunately, attempting to look up a non-existing keyword in a map will return
`nil`.  While sometimes convenient, this means that a simple typo in the keyword name will
silently return corrupted data (i.e. `nil`) instead of the desired value.

Instead, use the function grab for keyword/map lookup:
[source,clojure]
----
(grab k m)
  "A fail-fast version of keyword/map lookup.  When invoked as (grab :the-key the-map), 
   returns the value associated with :the-key as for (clojure.core/get the-map :the-key).  
   Throws an Exception if :the-key is not present in the-map."

user=> (def m {:a 1 :b 2})
user=> (grab :a m)
1
user=> (grab :c m)
;=> IllegalArgumentException Key not present in map:
;=>   map: {:b 2, :a 1}
;=>   key: :c
----
The function `grab` should also be used in place of `clojure.core/get`. Simply reverse the order of arguments to
match the "keyword-first, map-second" convention.

For looking up values in nested maps, the function `fetch` replaces `clojure.core/get-in`:
[source,clojure]
----
(fetch m ks)
  "A fail-fast version of clojure.core/get-in. When invoked as (fetch the-map keys-vec), 
   returns the value associated with keys-vec as for (clojure.core/get-in the-map keys-vec).  
   Throws an Exception if the path keys-vec is not present in the-map."

user=> (def mm {:a 1 :b {:c 3}})
user=> (fetch mm [:b :c])
3
user=> (fetch mm [:b :z])
;=> IllegalArgumentException Key seq not present in map:
;=>   map : {:b {:c 3}, :a 1}
;=>   keys: [:b :z]
----

==== Map Dissociation

Clojure has functions `assoc` & `assoc-in`, `update` & `update-in`, and `dissoc`. However, there
is no function `dissoc-in`.  The function `dissoc-entry` provides the desired functionality:

[source,clojure]
----
(dissoc-entry the-map keys-vec)
  "A sane version of dissoc-in that will not delete intermediate keys. 
   When invoked as (dissoc-entry the-map [:k1 :k2 :k3... :kZ]), acts like 
   (clojure.core/update-in the-map [:k1 :k2 :k3...] dissoc :kZ). That is, only 
   the map entry containing the last key :kZ is removed, and all map entries 
   higher than kZ in the hierarchy are unaffected."
----

The unit test shows the functions in action:

[source,clojure]
----
(let [mm {:a { :b { :c "c" }}} ]
  (is (= (dissoc-entry mm []         ) mm ))
  (is (= (dissoc-entry mm [:a      ] ) {} ))
  (is (= (dissoc-entry mm [:a :b   ] ) {:a {}} ))
  (is (= (dissoc-entry mm [:a :b :c] ) {:a { :b {}}} ))
  (is (= (dissoc-entry mm [:a :x :y] ) {:a { :b { :c "c" }
                                             :x nil }} ))
  (is (thrown? IllegalArgumentException  (dissoc-entry mm [] ))))
----

Note that if non-existant keys are included in `keys-vec` (except for `kZ`), any intervening map
layers will be constructed as necessary.

The name `dissoc-entry` is chosen to emphasize that only the map entry corresponding to the last key
`kZ` is cleared. This also avoids confusion with a function in the old
clojure-contrib library named `dissoc-in` that would sometimes delete keys above
`kZ` in the map hierarchy.

==== Map Entries (Key-Value pairs)

Sometimes you want to extract the keys & values from a map for manipulation or extension
before building up another map (especially useful for manipulating default function args).
Here is very handy function for that:

[source,clojure]
----
(keyvals m)
  For any map m, returns the keys & values of m as a vector, 
  suitable for reconstructing via (apply hash-map (keyvals m)).

=> (keyvals {:a 1 :b 2})
[:b 2 :a 1]
=> (apply hash-map (keyvals {:a 1 :b 2}))
{:b 2, :a 1}
----

==== Default value in case of Exception

Sometimes you know an operation may result in an Exception, and you would like to have the
Exception converted into a default value.  That is when you need:

[source,clojure]
----
(with-exception-default default-val & body)
  Evaluates body & returns its result.  In the event of an exception the
  specified default value is returned instead of the exception."

(with-exception-default 0
  (Long/parseLong "12xy3"))
0
----
=== Expression debugging

Ever been debugging some new code and had trouble printing out intermediate
values?  For example:

[source,clojure]
----
(-> 1
    (inc)
    (* 2))
4
----
Suppose you want to disply the value after the (inc) function. You can't just insert a
(println) because the return value of `nil` will break the pipeline structure. Instead,
just use `spy`:

[source,clojure]
----
(-> 1
    (inc)
    (spy :msg "after inc")
    (* 2))
;; after inc => 2
4
----
This tool is named `spy` since it can display values from inside either  a "thread-first" or a "thread-last" form
(e.g. using `->` or `->>`) where they would normally be hidden.  

[source,clojure]
----
(->> 1
    (inc)
    (spy :msg "after inc")
    (* 2))
;; after inc => 2
4
----

Note that the same call to `spy` works in either `->` or `->>` expressions.  The keyword `:msg` is required just
before the message string, as it is this that allows the `spy` function to detect whether
it is in a thread-first or thread-last form, and thus produce the correct output. As a bonus for debugging, the 
value is output using (pr-str ...) so that numbers and strings are unambiguous in the output:

[source,clojure]
----
> (-> 30 (+ 4) (spy :msg "dbg") (* 10))
;; dbg => 34
340

(-> "3" (str "4") (spy :msg "dbg") (str "0"))
;; dbg => "34"
"340"
----

Sometimes you may prefer to print out the literal expression instead of a
message. In this case, just use `spyx`:
[source,clojure]
----
(as-> 1 x
      (spyx (inc x))
      (* 2 x))
;; (inc x) => 2
4
----

In other instances, you may wish to use `spyxx` display both the type and value of an expression:
[source,clojure]
----
(defn mystery-fn [] (into (sorted-map) {:b 2 :a 1}))
(spyxx (mystery-fn))
;; (mystery-fn) => clojure.lang.PersistentTreeMap->{:a 1, :b 2}
----

To be precise, the function signatures are:

[source,clojure]
----
(spy :msg msg-str)    ; the ":msg" keyword literal is required
(spyx expr)
(spyxx expr)
----

=== REPL driven testing

Developing at the REPL is productive & cool, but it can be a pain to reload source files
modified in an external editor.  This is especially so when simultaneously editing both
the core program and the corresponding test code.  The function `test-all` will reload
both a namespace and its corresponding test namespace from file, then invoke the corresponding
test namespace:

[source,clojure]
----
(test-all & ns-names)
  Convenience fn to reload a namespace & the corresponding test namespace from disk and
  execute tests in the REPL.  Assumes canonical project test file organization with
  parallel src/... & test/tst/... directories, where a 'tst.' prefix is added to all src
  namespaces to generate the cooresponding test namespace.  Example:

    (test-all 'tupelo.core 'tupelo.csv)

  This will reload tupelo.core, tst.tupelo.core, tupelo.csv, tst.tupelo.csv and
  then execute clojure.test/run-tests on both of the test namespaces.
----

=== Floating Point Number Comparison

Everyone knows that you shouldn't compare floating-point numbers (e.g. float,
double, etc) for equality since roundoff errors can prevent a precise match
between logically equivalent results.  However, it has always been awkward to
regenerate "approx-equals" code by hand every time new project requires it.
Here we have a simple function that compares two floating-point values (cast to
double) for relative equality by specifying either the number of significant
digits that must match or the maximum error tolerance allowed:

[source,clojure]
----
(rel= val1 val2 & opts)
  Returns true if 2 double-precision numbers are relatively equal, else false.
  Relative equality is specified as either (1) the N most significant digits are
  equal, or (2) the absolute difference is less than a tolerance value.  Input
  values are coerced to double before comparison.  
----

An extract from the unit tests illustrates the use of `rel=`

[source,clojure]
----
  (is      (rel= 123450000 123456789 :digits 4 ))
  (is (not (rel= 123450000 123456789 :digits 6 )))

  (is      (rel= 1 1.001 :tol 0.01 ))
  (is (not (rel= 1 1.001 :tol 0.0001 )))
----
=== Full API Documentation

Is located at:  http://cloojure.github.io/doc/tupelo/

== Other Useful Tools:

Besides `tupelo.core`, tupelo has a number of other useful tools in the following
namespaces:

=== tupelo.csv - Functions for using CSV (Comma Separate Value) files

The standard link:http://github.com/davidsantiago/clojure-csv[clojure-csv library] has well-tested and useful functions for parsing 
CSV (Comma Separated Value) text data, but it does not offer all of the convenience one
may wish. In `tupelo.csv` we emphasize the idomatic Clojure usage of data, using
sequences and maps.

link:src/tupelo/csv.adoc[Documentation for tupelo.csv]

=== tupelo.parse - Functions to ease parsing

TEMP TODO:  see http://cloojure.github.io/tupelo/tupelo.parse.html

=== tupelo.base64 - Convert to/from base64 encoding

TEMP TODO:  see http://cloojure.github.io/tupelo/tupelo.base64.html

=== tupelo.y64 - Convert to/from the URL-safe Y64 encoding (Yahoo YUI library).

TEMP TODO:  see http://cloojure.github.io/tupelo/tupelo.y64.html

== License

Copyright Â© 2015 Alan Thompson. 

Distributed under the Eclipse Public License, the same as Clojure.
